buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.5.RELEASE'
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

allprojects {
    group = 'ru.ajana.admin'
    version = '1.0-SNAPSHOT'

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }

    apply plugin: 'idea'
    apply plugin: "eclipse"
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {         
        imports {
            mavenBom 'org.springframework:spring-framework-bom:5.1.3.RELEASE'
            mavenBom 'org.springframework.boot:spring-boot-dependencies:2.1.1.RELEASE'
            mavenBom 'org.springframework.security:spring-security-bom:5.1.3.RELEASE'
            mavenBom 'org.springframework.data:spring-data-releasetrain:Lovelace-SR3'
            mavenBom 'com.fasterxml.jackson:jackson-bom:2.9.8'
        }

        dependencies {
            dependency 'javax.inject:javax.inject:1'
            dependency 'org.eclipse.persistence:javax.persistence:2.1.1'
            dependency 'javax.validation:validation-api:2.0.1.Final'
            dependency 'org.hibernate:hibernate-validator:6.0.7.Final'
            dependency 'org.eclipse.persistence:eclipselink:2.7.1'
            dependency 'org.slf4j:slf4j-api:1.7.25'
            dependency 'org.slf4j:jcl-over-slf4j:1.7.25'
            dependency 'ch.qos.logback:logback-core:1.2.3'
            dependency 'ch.qos.logback:logback-classic:1.2.3'
            dependency 'com.h2database:h2:1.4.196'
            dependency 'org.mockito:mockito-core:2.15.0'
            dependency 'com.namics.oss:java-random:1.1.4'
            dependency 'junit:junit:4.12'
        }
    }

    task resolveAllDependencies {
        doLast {
            configurations.all { it.resolve() }
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: "idea"
    apply plugin: "eclipse"

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
    }

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

}